#version 460 core

#define STEPS 2

#define TRUE_STEPS (2 * STEPS)

#define WORKGROUP_X 8
#define WORKGROUP_Y 8
#define WORKGROUP ivec2(WORKGROUP_X, WORKGROUP_Y)

#define TRUE_WORKGROUP_X (2 * WORKGROUP_X)
#define TRUE_WORKGROUP_Y (2 * WORKGROUP_Y)
#define TRUE_WORKGROUP ivec2(TRUE_WORKGROUP_X, TRUE_WORKGROUP_Y)

#define WORKGROUP_X_OVERLAP (WORKGROUP_X + STEPS * 2)
#define WORKGROUP_Y_OVERLAP (WORKGROUP_Y + STEPS * 2)
#define WORKGROUP_OVERLAP ivec2(WORKGROUP_X_OVERLAP, WORKGROUP_Y_OVERLAP)

#define TRUE_WORKGROUP_X_OVERLAP (2 * WORKGROUP_X_OVERLAP)
#define TRUE_WORKGROUP_Y_OVERLAP (2 * WORKGROUP_Y_OVERLAP)
#define TRUE_WORKGROUP_OVERLAP ivec2(TRUE_WORKGROUP_X_OVERLAP, TRUE_WORKGROUP_Y_OVERLAP)

layout(local_size_x = WORKGROUP_X + STEPS * 2, local_size_y = WORKGROUP_Y + STEPS * 2) in;

#define CACHE_X (TRUE_WORKGROUP_X_OVERLAP + 2)
#define CACHE_Y (TRUE_WORKGROUP_Y_OVERLAP + 2)
#define CACHE_SIZE (CACHE_Y * CACHE_X)

// picture is stored in a row-major manner
// used both for read and write, boundary is not calculated
layout(binding = 0, r32f) uniform image2D solution[2];
layout(binding = 2, r32f) uniform readonly image2D f;

uniform int curr; // 0 or 1
uniform float w;
uniform float hx;
uniform float hy;

// first is x(i), second is y(j)
shared float cache[CACHE_SIZE];

int cacheFlatIndex(ivec2 indices)
{
	return indices.x * CACHE_Y + indices.y + (CACHE_Y + 1);
}

float cacheLoadValue(ivec2 indices)
{
	return cache[cacheFlatIndex(indices)];
}

void cacheStoreValue(ivec2 indices, float value)
{
	cache[cacheFlatIndex(indices)] = value;
}

// returns local index(zero-based), global(can be out of bounds)
void getGlobalLocalInvocationID(out ivec2 global, out ivec2 local)
{
	ivec2 work = ivec2(gl_WorkGroupID.xy);

	local = 2 * ivec2(gl_LocalInvocationID.xy);
	global = local - TRUE_STEPS + work * TRUE_WORKGROUP;
}

// step function, in fact return signed distance to the frame defined by start coord and end coord
int stepFunction(ivec2 coord, ivec2 start, ivec2 end)
{
	ivec2 dc1 = coord - start;
	ivec2 dc2 = end - coord;
	return min(min(dc1.x, dc1.y), min(dc2.x, dc2.y));
}

int getCurrStep()
{
	return stepFunction(ivec2(gl_LocalInvocationID.xy), ivec2(0), WORKGROUP_OVERLAP - 1);
}

// some invocation parameters
bool inRegion(ivec2 coords, ivec2 start, ivec2 end)
{
	return all(lessThanEqual(start, coords)) && all(lessThan(coords, end));
}

bool inBounds(ivec2 global, ivec2 size)
{
	return inRegion(global, ivec2(0), size);
}

bool onBoundary(ivec2 global, ivec2 size)
{
	return any(equal(global, ivec2(0))) || any(equal(global, size - 1)); 
}

// red-black step
float update(float u00, float um10, float u10, float u0m1, float u01, float f00)
{
	float hxhx = hx * hx;
	float hyhy = hy * hy;
	float H = -2.0 / hxhx - 2.0 / hyhy;
	float u = f00 / H - (um10 + u10) / (hxhx * H) - (u0m1 + u01) / (hyhy * H);

	return (1.0 - w) * u00 + w * u;
}

void main()
{
	// some invocation's parameters
	ivec2 global, local;
	getGlobalLocalInvocationID(global, local);

	ivec2 size = imageSize(solution[curr]);

	float u00Updateable = float(inBounds(global              , size) && !onBoundary(global              , size));
	float u10Updateable = float(inBounds(global + ivec2(1, 0), size) && !onBoundary(global + ivec2(1, 0), size));
	float u01Updateable = float(inBounds(global + ivec2(0, 1), size) && !onBoundary(global + ivec2(0, 1), size));
	float u11Updateable = float(inBounds(global + ivec2(1, 1), size) && !onBoundary(global + ivec2(1, 1), size));

	int steps = (inBounds(global, size) ? getCurrStep() : -1);

	// cache data
	float f01, f11;
	float f00, f10;

	// loads either value or zero if out of bounds (check optimized out)
	f00 = imageLoad(f, global              ).x; 
	f10 = imageLoad(f, global + ivec2(1, 0)).x;
	f01 = imageLoad(f, global + ivec2(0, 1)).x;
	f11 = imageLoad(f, global + ivec2(1, 1)).x;

	// solution data
	float u01, u11;
	float u00, u10;

	// loads either value or zero if out of bounds (check optimized out)
	u00 = imageLoad(solution[curr], global              ).x;
	u10 = imageLoad(solution[curr], global + ivec2(1, 0)).x;
	u01 = imageLoad(solution[curr], global + ivec2(0, 1)).x;
	u11 = imageLoad(solution[curr], global + ivec2(1, 1)).x;

	// cache store
	cacheStoreValue(local              , u00);
	cacheStoreValue(local + ivec2(1, 0), u10);
	cacheStoreValue(local + ivec2(0, 1), u01);
	cacheStoreValue(local + ivec2(1, 1), u11);

	barrier();
	memoryBarrierShared();
	
	// computations	
	for (int i = 1; i <= STEPS; i++, steps--) {	
		// black update
		float u20  = cacheLoadValue(local + ivec2(2,  0)); // right
		float u1m1 = cacheLoadValue(local + ivec2(1, -1)); // bottom

		float u02  = cacheLoadValue(local + ivec2( 0, 2)); // top
		float um11 = cacheLoadValue(local + ivec2(-1, 1)); // left

		float u10_new = update(u10, u00, u20, u1m1, u11, f10);
		float u01_new = update(u01, um11, u11, u00, u02, f01);

		u10 = u10 + (u10_new - u10) * u10Updateable;
		u01 = u01 + (u01_new - u01) * u01Updateable;
		if (steps >= 0) {
			cacheStoreValue(local + ivec2(1, 0), u10);
			cacheStoreValue(local + ivec2(0, 1), u01);
		}

		barrier();
		memoryBarrierShared();

		// red update
		float u0m1 = cacheLoadValue(local + ivec2( 0, -1)); // bottom
		float um10 = cacheLoadValue(local + ivec2(-1,  0)); // left

		float u12 = cacheLoadValue(local + ivec2(1, 2)); // top
		float u21 = cacheLoadValue(local + ivec2(2, 1)); // right

		float u00_new = update(u00, um10, u10, u0m1, u01, f00);
		float u11_new = update(u11, u01, u21, u10, u12, f11);

		u00 = u00 + (u00_new - u00) * u00Updateable;
		u11 = u11 + (u11_new - u11) * u11Updateable;
		if (steps > 0)	{
			cacheStoreValue(local + ivec2(0, 0), u00);
			cacheStoreValue(local + ivec2(1, 1), u11);
		}

		barrier();
		memoryBarrierShared();
	}
	
	if (steps >= 0) { // out of bound writes are ignored, u[01][01] are already updated
		imageStore(solution[curr ^ 1], global              , vec4(u00));
		imageStore(solution[curr ^ 1], global + ivec2(1, 0), vec4(u10));
		imageStore(solution[curr ^ 1], global + ivec2(0, 1), vec4(u01));
		imageStore(solution[curr ^ 1], global + ivec2(1, 1), vec4(u11));
	}
}
