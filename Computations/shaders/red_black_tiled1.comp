#version 460 core

#define STEPS 2

#define TRUE_STEPS (2 * STEPS)

#define WORKGROUP_X 8
#define WORKGROUP_Y 8
#define WORKGROUP ivec2(WORKGROUP_X, WORKGROUP_Y)

#define TRUE_WORKGROUP_X (2 * WORKGROUP_X)
#define TRUE_WORKGROUP_Y (2 * WORKGROUP_Y)
#define TRUE_WORKGROUP ivec2(TRUE_WORKGROUP_X, TRUE_WORKGROUP_Y)

#define WORKGROUP_X_OVERLAP (WORKGROUP_X + STEPS * 2)
#define WORKGROUP_Y_OVERLAP (WORKGROUP_Y + STEPS * 2)
#define WORKGROUP_OVERLAP ivec2(WORKGROUP_X_OVERLAP, WORKGROUP_Y_OVERLAP)

#define TRUE_WORKGROUP_X_OVERLAP (2 * WORKGROUP_X_OVERLAP)
#define TRUE_WORKGROUP_Y_OVERLAP (2 * WORKGROUP_Y_OVERLAP)
#define TRUE_WORKGROUP_OVERLAP ivec2(TRUE_WORKGROUP_X_OVERLAP, TRUE_WORKGROUP_Y_OVERLAP)

layout(local_size_x = WORKGROUP_X + STEPS * 2, local_size_y = WORKGROUP_Y + STEPS * 2) in;

#define CACHE_X (TRUE_WORKGROUP_X_OVERLAP + 2)
#define CACHE_Y (TRUE_WORKGROUP_Y_OVERLAP + 2)
#define CACHE_SIZE (CACHE_Y * CACHE_X)

// picture is stored in a row-major manner
// used both for read and write, boundary is not calculated
layout(binding = 0, r32f) uniform image2D solution[2];
layout(binding = 2, r32f) uniform readonly image2D f;

uniform int curr; // 0 or 1
uniform float w;
uniform float hx;
uniform float hy;

// first is x(i), second is y(j)
shared float cache[CACHE_SIZE];

int cacheFlatIndex(ivec2 indices)
{
	return indices.x * CACHE_Y + indices.y + (CACHE_Y + 1);
}

float cacheLoadValue(ivec2 indices)
{
	return cache[cacheFlatIndex(indices)];
}

void cacheStoreValue(ivec2 indices, float value)
{
	cache[cacheFlatIndex(indices)] = value;
}

// returns local index(zero-based), global(can be out of bounds)
void getGlobalLocalInvocationID(out ivec2 global, out ivec2 local)
{
	ivec2 work = ivec2(gl_WorkGroupID.xy);

	local = 2 * ivec2(gl_LocalInvocationID.xy);
	global = local - ivec2(TRUE_STEPS) + work * TRUE_WORKGROUP;
}

int getCurrStep()
{
	int dx1 = int(gl_LocalInvocationID.x);
	int dy1 = int(gl_LocalInvocationID.y);
	int dx2 = (WORKGROUP_X_OVERLAP - 1) - dx1;
	int dy2 = (WORKGROUP_Y_OVERLAP - 1) - dy1;

	return min(min(dx1, dy1), min(dx2, dy2));
}

// some invocation parameters
bool inRegion(ivec2 coords, ivec2 start, ivec2 end)
{
	return start.x <= coords.x && coords.x < end.x 
		&& start.y <= coords.y && coords.y < end.y;
}

bool inBounds(ivec2 global, ivec2 size)
{
	return inRegion(global, ivec2(0), size);
}

bool onBoundary(ivec2 global, ivec2 size)
{
	return global.x == 0 || global.x == size.x - 1 
		|| global.y == 0 || global.y == size.y - 1;
}

void getOrient(ivec2 local, int steps, out bool lt, out bool rb)
{
	steps *= 2;

	int x0 = steps;
	int x1 = TRUE_WORKGROUP_X_OVERLAP - 2 - steps;
	int y0 = steps;
	int y1 = TRUE_WORKGROUP_Y_OVERLAP - 2 - steps;

	rb = (y0 < local.y && local.y < y1 && local.x == x1) || (x0 < local.x && local.x < x1 && local.y == y0);
	lt = (y0 < local.y && local.y < y1 && local.x == x0) || (x0 < local.x && local.x < x1 && local.y == y1);
}

// red-black step
float update(float u00, float um10, float u10, float u0m1, float u01, float f00)
{
	float hxhx = hx * hx;
	float hyhy = hy * hy;
	float H = -2.0 / hxhx - 2.0 / hyhy;
	float u = f00 / H - (um10 + u10) / (hxhx * H) - (u0m1 + u01) / (hyhy * H);

	return (1.0 - w) * u00 + w * u;
}


void main()
{
	// some invocation's parameters
	ivec2 global, local;
	getGlobalLocalInvocationID(global, local);

	ivec2 size = imageSize(solution[curr]);

	bool globalInBounds = inBounds(global, size);

	bool u00NotOnBound = !onBoundary(global + ivec2(0, 0), size);
	bool u10NotOnBound = !onBoundary(global + ivec2(1, 0), size);
	bool u01NotOnBound = !onBoundary(global + ivec2(0, 1), size);
	bool u11NotOnBound = !onBoundary(global + ivec2(1, 1), size);

	int steps = (globalInBounds ? getCurrStep() : -1);

	bool lt, rb;
	getOrient(local, steps, lt, rb);

	// cache data
	float f01, f11;
	float f00, f10;

	f00 = imageLoad(f, global + ivec2(0, 0)).x; 
	f10 = imageLoad(f, global + ivec2(1, 0)).x;
	f01 = imageLoad(f, global + ivec2(0, 1)).x;
	f11 = imageLoad(f, global + ivec2(1, 1)).x;

	float u01, u11;
	float u00, u10;

	u00 = imageLoad(solution[curr], global + ivec2(0, 0)).x;
	u10 = imageLoad(solution[curr], global + ivec2(1, 0)).x;
	u01 = imageLoad(solution[curr], global + ivec2(0, 1)).x;
	u11 = imageLoad(solution[curr], global + ivec2(1, 1)).x;

	if (steps >= 0)
	{
		cacheStoreValue(local + ivec2(0, 0), u00);
		cacheStoreValue(local + ivec2(1, 0), u10);
		cacheStoreValue(local + ivec2(0, 1), u01);
		cacheStoreValue(local + ivec2(1, 1), u11);
	}
	barrier();
	memoryBarrierShared();
	
	// computations	
	for (int i = 1; i <= STEPS; i++, steps--)
	{	
		// black update
		float u20  = cacheLoadValue(local + ivec2(2,  0)); // right
		float u1m1 = cacheLoadValue(local + ivec2(1, -1)); // bottom

		float u02  = cacheLoadValue(local + ivec2( 0, 2)); // top
		float um11 = cacheLoadValue(local + ivec2(-1, 1)); // left

		if (steps >= 0)
		{
			float u10_new = update(u10, u00, u20, u1m1, u11, f10);
			if (u10NotOnBound)
			{
				u10 = u10_new;
			}
			cacheStoreValue(local + ivec2(1, 0), u10);

			float u01_new = update(u01, um11, u11, u00, u02, f01);
			if (u01NotOnBound)
			{
				u01 = u01_new;
			}
			cacheStoreValue(local + ivec2(0, 1), u01);
		}
		barrier();
		memoryBarrierShared();


		// red update
		float u0m1 = cacheLoadValue(local + ivec2( 0, -1)); // bottom
		float um10 = cacheLoadValue(local + ivec2(-1,  0)); // left

		float u12 = cacheLoadValue(local + ivec2(1, 2)); // top
		float u21 = cacheLoadValue(local + ivec2(2, 1)); // right

		if (steps > 0)
		{
			float u00_new = update(u00, um10, u10, u0m1, u01, f00);
			if (u00NotOnBound)
			{
				u00 = u00_new;
			}
			cacheStoreValue(local + ivec2(0, 0), u00);

			float u11_new = update(u11, u01, u21, u10, u12, f11);
			if (u11NotOnBound)
			{
				u11 = u11_new;
			}
			cacheStoreValue(local + ivec2(1, 1), u11);
		}
		barrier();
		memoryBarrierShared();
	}
	
	if (steps >= 0)
	{
		imageStore(solution[curr ^ 1], global              , vec4(cacheLoadValue(local              )));
		imageStore(solution[curr ^ 1], global + ivec2(1, 0), vec4(cacheLoadValue(local + ivec2(1, 0))));
		imageStore(solution[curr ^ 1], global + ivec2(0, 1), vec4(cacheLoadValue(local + ivec2(0, 1))));
		imageStore(solution[curr ^ 1], global + ivec2(1, 1), vec4(cacheLoadValue(local + ivec2(1, 1))));
	}
}
